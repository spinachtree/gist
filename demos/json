
JSON -- see: http://www.json.org/

gist
	json   =  s (object | array) s 
	object = '{' s pairs? s '}'
	pairs  =  pair (s ',' s pair)*
	pair   = string s ':' s val
	array  = '[' s vals? s ']'
	vals   =  val (s ',' s val)*
	val    = object|array|string|number
	         |true|false|null
	string = quot (chs|esc)* quot
	esc    = bs (code | 'u' hex hex hex hex)
	number = neg? digits frac? exp?
	frac   : '.' int
	exp    : ('e'|'E') sign? int
	digits : '0'|'1'..'9' digit*
	int    : digit+
	digit  : '0'..'9'
	sign   : '+'|'-'
	neg    :  '-'
	true   : 'true'
	false  : 'false'
	null   : 'null'
	hex    : digit|'a'..'f'|'A'..'F'
	code   : bs|fs|quot|'b'|'f'|'n'|'r'|'t'
	bs     : 92   -- back-slash
	fs     : 47   -- forward-slash
	quot   : 34   -- quote
	s      : (9|10|13|32)*
	chs    : char+
	char   : 0x20..10ffff^quot^bs

parse
	{ "a" : true, "b" : false, "list" : [123.45, "one two", null] }

parse
	{ "Image" : {
		"Width":  800,
		"Height": 600,
		"Title": "View from:\t15th Floor",
		"Thumbnail": {
			"Url":  "http://www.example.com/image/481989943",
			"Height": 125,
			"Width":  "100"
			}
		},
	  "IDs": [116, 943, 234, 38793]
	}
